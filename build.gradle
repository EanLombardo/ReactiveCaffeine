buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1+"
        classpath 'org.ajoberstar:gradle-git:1.4.2'
    }
}

def VERSION_CODE = '0.0.5'

group 'com.rxc'
version VERSION_CODE

task wrapper(type: Wrapper) {
  gradleVersion = '2.5'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'org.ajoberstar.github-pages'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.powermock:powermock-mockito-release-full:1.5.4'

    compile 'io.reactivex:rxjava:1.0.14'
}

bintray {
    user = hasProperty('bintrayUser') ? property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = hasProperty('bintrayApiKey') ? property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    filesSpec {
        from 'build/libs'
        into '.'
    }
    pkg {
        repo = 'maven'
        name = 'ReactiveCaffeine'
        desc = 'Some handy tools for RXJava'
        licenses = ['Apache-2.0']
        vcsUrl = 'git@github.com:EanLombardo/ReactiveCaffeine.git'
        labels = ['rx', 'java', 'rxjava']
        publicDownloadNumbers = true
        publications = ['MyPublication']
        version {
            name = VERSION_CODE
            desc = ''
            vcsTag = VERSION_CODE
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'reactive-caffeine'
            artifactId 'reactive-caffeine'
            version VERSION_CODE
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

githubPages {
    repoUri = 'git@github.com:EanLombardo/ReactiveCaffeine.git'
    pages {
        from javadoc
    }
}